;mode800
*define

caption "魔女魔少魔法魔 for ONScripter"

rmenu "記憶",save,"追想",load,"疾駆",skip,"隠蔽",windowerase,"冒頭",reset
savename "セーブ","ロード","データ"
savenumber 18
transmode alpha
globalon
rubyon
nsa
humanz 10
windowback


pretextgosub *pretext_lb
loadgosub *loadlb
defsub title_mmmm
defsub rsf

effect  8,10,1000
effect  9,10,200
effect 10,10,500

;<<-EFFECT->>

game
;----------------------------------------
*pretext_lb
	lsp 10,":s/24,24,0;#ffffff"+$11,32,440 ;名前の表示
	saveon
return

*loadlb
	setwindow3 32,470,30,4,24,24,0,2,20,1,1,#999999,16,424,783,583
return

*title_mmmm
	bg "data\image\魔女魔少魔法魔1.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔2.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔3.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔4.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔5.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔6.png",9
	wait 200
	bg "data\image\魔女魔少魔法魔7.png",9
	wait 1000
	
	dwave 0,"data\sound\きしみ2.ogg"
	
	bg "data\image\魔女魔少魔法魔7'.png",8
	wait 1000
	bg "data\image\魔女魔少魔法魔7''.png",9
	wait 1000
	bg black,8
return

*rsf
	setwindow3 32,470,30,4,24,24,0,2,20,1,1,#999999,16,424,783,583
return
;----------------------------------------
*staffroll
	
	;%150 再生時間
	;%151 ロール画像x - 使わん
	;%152 ロール画像y
	;%153 gettimer
	;%154 下記参照
	
	;zigoku11endの再生時間
	mov %150,110078
	
	lsp 13,"data\staffroll_create.png",0,0
	getspsize 13,%151,%152
	sub %152,600
	
	resettimer
	
	*staffroll_loop
		gettimer %153
		
		;(経過時間/再生時間)*ロール画像y
		mov %154,%153*%152/%150
		
		if %153<%150 amsp 13,0,0-%154:print 1
		if %153>%150 bgmstop:amsp 13,0,0-%152:print 1:goto *staffroll_end
	goto *staffroll_loop
	*staffroll_end
return
;----------------------------------------
*start

rsf

;debug
;mov %200,1

;初回起動時 - 音量用変数すべて100
fileexist %130,"gloval.sav"
if %130==0 mov %230,100:mov %231,100:mov %232,100

bgmvol   60*%230/100
sevol    60*%231/100
voicevol 60*%232/100

setcursor 0,":a/11,80,2;data\system\LineBreak_a.png"
setcursor 1,":a/11,80,2;data\system\PageBreak_a.png"

bg "data\image\title.png",8
wait 1000
bg black,8

bgm "data\bgm\zigoku10.ogg"

if %200!=1 bg "data\image\mgirlm_title1.png",8
if %200==1 bg "data\image\mgirlm_title_B_86.png",8

if %200!=1 lsp 11,":a/3,0,3;data\image\start_mgirlm.png" ,666+24,344+24
if %200!=1 lsp 12,":a/3,0,3;data\image\load_mgirlm.png"  ,666+24,399+24
if %200!=1 lsp 13,":a/3,0,3;data\image\config_mgirlm.png",667+24,454+24
if %200!=1 lsp 14,":a/3,0,3;data\image\music_mgirlm.png" ,666+24,509+24

if %200==1 lsp 11,":a/3,0,3;data\image\start_mgirlm_B_86.png" ,666+24,344+24
if %200==1 lsp 12,":a/3,0,3;data\image\load_mgirlm_B_86.png"  ,666+24,399+24
if %200==1 lsp 13,":a/3,0,3;data\image\config_mgirlm_B_86.png",667+24,454+24
if %200==1 lsp 14,":a/3,0,3;data\image\music_mgirlm_B_86.png" ,666+24,509+24

lsp 15,":s#FFFFFF#888888Ｘ",775,2

print 1

*title_loop
	bclear
	
	spbtn 11,11
	spbtn 12,12
	spbtn 13,13
	spbtn 14,14
	spbtn 15,15
	
	btnwait %0
	
	if %0==11 dwave 0,"data\sound\凛.ogg":bgmstop:csp -1:goto *scenario
	if %0==12 bgmstop:csp -1:bg "data\image\title1.png",1:systemcall load:reset
	if %0==13 bgmstop:csp -1:bg black,9:goto *volmenu_GUI
	;if %0==14 bgmstop:csp -1:bg black,9:bgm "data\bgm\zigoku11end.ogg":gosub *staffroll:reset	;スタッフロールテスト
	if %0==15 bgmstop:csp -1:bg black,9:end
	
goto *title_loop
;----------------------------------------
*volmenu_GUI
	;https://gist.github.com/Prince-of-sea/325b8ae6912ecf23316a71c3d008480c
	;文字/数字/スプライト/ボタン
	;全部130~149までを使ってます - 競合に注意
	
	;バー文字列定義
	mov $130,":s;#FFFFFF#666666○――――――――――"
	mov $131,":s;#FFFFFF#666666―○―――――――――"
	mov $132,":s;#FFFFFF#666666――○――――――――"
	mov $133,":s;#FFFFFF#666666―――○―――――――"
	mov $134,":s;#FFFFFF#666666――――○――――――"
	mov $135,":s;#FFFFFF#666666―――――○―――――"
	mov $136,":s;#FFFFFF#666666――――――○――――"
	mov $137,":s;#FFFFFF#666666―――――――○―――"
	mov $138,":s;#FFFFFF#666666――――――――○――"
	mov $139,":s;#FFFFFF#666666―――――――――○―"
	mov $140,":s;#FFFFFF#666666――――――――――○"
	
*volmenu_loop
	
	;文字列変換
	itoa2 $141,%230
	itoa2 $142,%231
	itoa2 $143,%232
	
	;バー代入
	if %230==  0 mov $146,$130
	if %230== 10 mov $146,$131
	if %230== 20 mov $146,$132
	if %230== 30 mov $146,$133
	if %230== 40 mov $146,$134
	if %230== 50 mov $146,$135
	if %230== 60 mov $146,$136
	if %230== 70 mov $146,$137
	if %230== 80 mov $146,$138
	if %230== 90 mov $146,$139
	if %230==100 mov $146,$140
	if %231==  0 mov $147,$130
	if %231== 10 mov $147,$131
	if %231== 20 mov $147,$132
	if %231== 30 mov $147,$133
	if %231== 40 mov $147,$134
	if %231== 50 mov $147,$135
	if %231== 60 mov $147,$136
	if %231== 70 mov $147,$137
	if %231== 80 mov $147,$138
	if %231== 90 mov $147,$139
	if %231==100 mov $147,$140
	if %232==  0 mov $148,$130
	if %232== 10 mov $148,$131
	if %232== 20 mov $148,$132
	if %232== 30 mov $148,$133
	if %232== 40 mov $148,$134
	if %232== 50 mov $148,$135
	if %232== 60 mov $148,$136
	if %232== 70 mov $148,$137
	if %232== 80 mov $148,$138
	if %232== 90 mov $148,$139
	if %232==100 mov $148,$140
	
	;画面作成
	lsp 130,":s;#FFFFFF［Ｃｏｎｆｉｇ］", 50, 50
	lsp 131,":s;#FFFFFF#666666リセット", 400,450
	lsp 132,":s;#FFFFFF#666666戻る",     550,450
	
	lsp 135,":s;#FFFFFFＢＧＭ",           50,150
	lsp 136,":s;#FFFFFF#666666＜",       200,150
	lsp 137,$146,                        250,150
	lsp 138,":s;#FFFFFF#666666＞",       550,150
	lsp 139,":s;#FFFFFF#666666"+$141,    600,150
	
	lsp 140,":s;#FFFFFFＳＥ１",           50,250
	lsp 141,":s;#FFFFFF#666666＜",       200,250
	lsp 142,$147,                        250,250
	lsp 143,":s;#FFFFFF#666666＞",       550,250
	lsp 144,":s;#FFFFFF#666666"+$142,    600,250
	
	lsp 145,":s;#FFFFFFＳＥ２",           50,350
	lsp 146,":s;#FFFFFF#666666＜",       200,350
	lsp 147,$148,                        250,350
	lsp 148,":s;#FFFFFF#666666＞",       550,350
	lsp 149,":s;#FFFFFF#666666"+$143,    600,350
	
	print 1
	
	;ボタン定義
	bclear
	spbtn 131,131
	spbtn 132,132
	spbtn 136,136
	spbtn 138,138
	spbtn 141,141
	spbtn 143,143
	spbtn 146,146
	spbtn 148,148
	
	;入力待ち
	btnwait %140
	
	if %140==131 mov %230,100:mov %231,100:mov %232,100
	if %140==132 csp -1:reset
	if %140==136 if %230!=  0 sub %230,10
	if %140==138 if %230!=100 add %230,10
	if %140==141 if %231!=  0 sub %231,10
	if %140==143 if %231!=100 add %231,10
	if %140==146 if %232!=  0 sub %232,10
	if %140==148 if %232!=100 add %232,10
	
goto *volmenu_loop
;----------------------------------------
*scenario
